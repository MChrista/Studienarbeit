/*                                       */
/* 'ldscript' --- A simple Linker Script */
/* for use with the GNU linker 'ld' when */ 
/* producing a binary-format executable. */
/*                                       */
/* usage:                                */
/*   $ gcc -c main.cpp                   */
/*   $ ld main.o -T ldscript -o main.b   */

/* programmer: ALLAN CRUSE */
/* written on: 02 MAY 2004 */
/* revised on: 24 MAY 2004 */

SEARCH_DIR("../libkernel/obj");
OUTPUT_FORMAT("elf32-i386");
ENTRY(_start);

LD_DATA_ADDR = 0x10000;
LD_BSS_ADDR  = 0x18000;
LD_TEXT_START = 0x10000;
LD_DATA_START = LD_TEXT_START + LD_DATA_ADDR;
LD_BSS_START  = LD_TEXT_START + LD_BSS_ADDR;

SECTIONS
{
    .signature 0x00000000 : AT(0x00000000)
    {
        start.o(.signature)
    }
    .note.gnu.build-id : { *(.note.gnu.build-id) }
    .text16 :
    {
        start.o(.text16)
        *(.text16)
        . = ALIGN(16);
    }
    .text :
    {
        code = .; _code = .; __code = .;
        start.o(.text)
        *(.text)
        PROVIDE(etext = .);
    }
    .data 0x00000000 : AT(LD_DATA_ADDR)
    {
        data = .; _data = .; __data = .;
        sdata = ADDR(.data);
        *(.data)
        *(.rodata)
        PROVIDE(edata = .);
        . = ALIGN(16);
    }
    .bss :
    {
        bss = .; _bss = .; __bss = .;
        *(.bss)
        PROVIDE(ebss = .);
        . = ALIGN(16);
    }
    end = .; _end = .; __end = .;
}

